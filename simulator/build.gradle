plugins {
	id 'java'
	id 'idea'
	id 'com.github.johnrengelman.shadow' version '2.0.4'
}

sourceCompatibility = 1.8

group 'simulator'
version getGitHash()

repositories {
	mavenCentral()
}

project.ext {
	junit = '4.12'
	hamcrest = '1.3'
	jfreechart = '1.5.0'
}

dependencies {

	// peersim core
	compile files('libs/jep-2.3.0.jar')
	compile files('libs/djep-1.0.0.jar')
	compile files('libs/peersim-1.0.5.jar')

	// test utilities
	testCompile group: 'junit', name: 'junit', version: project.ext.junit
	testCompile group: 'org.hamcrest', name: 'hamcrest-all', version: project.ext.hamcrest
	testCompile group: 'org.jfree', name: 'jfreechart', version: project.ext.jfreechart
}

jar {
	manifest {
		attributes(
			'Main-Class': 'peersim.rangesim.RangeSimulator'
		)
	}
}

shadowJar {
	archiveName = "simulator.jar"
}

final def getGitHash() {
	final def stdout = new ByteArrayOutputStream()
	exec {
		commandLine 'git', 'rev-parse', 'HEAD'
		standardOutput = stdout
	}
	return stdout.toString().trim()
}
